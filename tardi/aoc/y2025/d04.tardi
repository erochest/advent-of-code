
uses: ../io
uses: std/io
uses: std/hashmaps
uses: std/math
uses: std/strings
uses: std/vectors

exports:
  parse-input
  matrix-indexes
  dimensions
  around
  combine
  walk-matrix
  surrounding-coordinates
  surrounding-sum
  filter
  matrix-nth
  is-set?
  accessable?
  debug-part1
  part1
  part2 ;

: >code         64 == 1 0 ? ;
: parse-line    >utf8 [ >code ] map ;
: parse-input   lines [ parse-line ] map ;   /// str -- vector/vector/bit

/// matrix -- width height
: dimensions   [ first vectors/length ] [ vectors/length ] bi ;

// TODO: std/vectors
: matrix-indexes   dimensions [ [0..) ] bi@ ;   /// vector/vector -- xs ys

/// x --
: debug   print " " print .s ;

/// x -- x
: debug-col   [ print ] keep "\t" print ;

/// matrix lambda( matrix xy -- z ) -- zs
: walk-matrix
  swap dup matrix-indexes combine
  [
    over
    [ rot [ apply ] keep ] dip
    rot
  ] map
  2dropd ;

: around   [ 1- ] [ 1+ ] bi [..] ;   /// n -- ns

: surrounding-all        [ first2 [ around ] bi@ combine ] keep ;   /// xy -- xys xy
: surrounding-not-self   [ dupd != ] filter nip ;                   /// xy xys -- xys'

/// x/#f -- ?
: succeeds?   #f != ;

: make-check-inclusive   [ 1- ] dip ;   /// d x -- d' x
: insert-lower-bound     0 rot ;        /// h x -- 0 h x

/// wh xy accessor -- ?
: check-bounds-for
  bi@
  make-check-inclusive
  insert-lower-bound
  check-bounds
  succeeds? ;

: in-width?    [ first ]  check-bounds-for ;   /// dims xy
: in-height?   [ second ] check-bounds-for ;   /// dims xy

: on-grid?               [ in-width? ] [ in-height? ] 2bi and ;   /// hw xy -- ?
: surrounding-off-grid   [ dupd on-grid? ] filter nip ;           /// dims xys -- xys'

/// { width height } { x y } -- vector/pairs
: surrounding-coordinates
  surrounding-all
  swap surrounding-not-self
  surrounding-off-grid ;

: matrix-nth   [ second swap nth ] [ first swap nth ] bi ;   /// matrix xy -- n

: is-set?   [ matrix-nth ] 2keep rot zero? ! ;   /// m xy -- m xy ?

/// matrix xy -- int
: surrounding-sum
  is-set? [
    dupd [ dimensions 2vector ] dip
    surrounding-coordinates
    [ dupd matrix-nth ] map
    sum nip
  ] [
    2drop 0
  ] if ;

/// n -- ?
: accessable?   [ 0 > ] [ 4 < ] bi and ;

/// data/str -- checksum/int
: part1
  parse-input
  [ surrounding-sum ] walk-matrix
  [ accessable? ] filter
  vectors/length ;

/// data/str -- checksum/int
: debug-part1
  parse-input
  [
    is-set?
    [
      debug-col
      dupd [ dimensions 2vector ] dip surrounding-coordinates
      debug-col
      [ dupd matrix-nth ] map
      debug-col
      sum nip
      debug-col nl
    ] [
      2drop 0
    ] if
  ] walk-matrix
  [ 4 < ] filter
  vectors/length ;

/// -- checksum/int
: run-sample-1   2025 04 read-sample-data part1 ;
: run-data-1     2025 04 read-data        part1 ;

///////////////////////////

/// data/str -- checksum/int
: part2   ;

/// -- checksum/int
: run-sample-2   2025 04 read-sample-data part2 ;
: run-data-2     2025 04 read-data        part2 ;

// run-sample-1 .
// run-data-1 .
// run-sample-2 .
// run-data-2 .
