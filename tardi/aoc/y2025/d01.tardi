
uses: ../io
uses: std/io
uses: std/hashmaps
uses: std/math
uses: std/strings
uses: std/vectors

exports:
  mod
  left right
  turn-dial
  zero? count-zero
  accum-dial
  parse-input ??? part1 run-data-1
  left-zeros right-zeros
  turn-dial-zero accum-dial-zero
  ???-??? moar-profit part2
  debug ;

// `mod` not built in yet :facepalm:
/// x y -- x%y
: mod
    [ 2dup >= ] [
        [ - ] keep
    ] while
    drop ;

/// value -- value'
: full-spins   [ dup 100 > ] [ 100 - ] while ;

/// dial n -- dial'
: left
    full-spins
    [ 100 + ] dip - abs
    100 mod ;

/// dial n -- dial'
: right
    full-spins
    +
    100 mod ;

/// line -- pair
: parse-line
    1 split-at
    parse-int
    2vector ;

/// data/str -- xs
: parse-input   lines [ parse-line ] map ;

: left?   "L" == ;

/// n line-pair -- n'
: turn-dial
    first2 swap
    left?
    [ left ] [ right ] if ;

// TODO: std/math
/// n -- ?
: zero?   0 == ;

/// count n -- count'
: count-zero   zero? [ 1+ ] when ;

: get-current-dial   [ dup last ] dip ;   /// dial-log x -- dial-log current x
: log-dial           over push! ;         /// dial-log next -- dial-log'

/// history pair -- history'
: accum-dial
    get-current-dial
    turn-dial
    log-dial ;

/// pairs -- dial-settings
: ???   { 50 } [ accum-dial ] reduce ;

/// ys -- checksum/int
: profit   [ zero? ] filter vectors/length ;

/// vector n -- vector to from
: indexes-from-end
    over vectors/length
    swap dupd - ;

: invalid-start-index   0 <= ;    /// i -- ?
: cleanup-indexes       2drop ;   /// to from --

// TODO: std/vectors
/// xs n -- xs'
: lastn
    indexes-from-end
    dup invalid-start-index [ cleanup-indexes ] [
        spin subvector
    ] if ;

/// x -- x
: print-col   "\t" print dup print ;

/// lines -- zero-count
: debug
    { 50 } [
        // log line -- log'
        dup print
        parse-line
        print-col
        accum-dial
        dup 10 lastn print-col
        last zero? print-col
        nl drop
    ] reduce
    profit ;

/// data/str -- checksum/int
: part1   parse-input ??? profit ;

/// -- checksum/int
: run-sample-1   2025 01 read-sample-data part1 ;
: run-data-1     2025 01 read-data        part1 ;

// run-data-1 .
// 2025 01 read-data lines debug
// 2025 01 read-sample-data lines debug

///////////////////////////

: pad-zero-dial   [ dup zero? [ drop 100 ] when ] dip ;   /// n x -- n' x

: (spin-left)   100 + ;   /// n -- n'

/// dial -- dial' spin-count
: spin-left
    0
    [ over 0 < ] [
        [ (spin-left) ] dip 1+
    ] while
    [ 100 mod ] dip ;

: on-zero-1+   over zero? [ 1+ ] when ;   /// dial spin-count -- dial spin-count'

/// dial n -- dial' zero-count
: left-zeros
    pad-zero-dial -
    spin-left
    on-zero-1+ ;

: (spin-right)   100 - ;   /// n -- n'

/// dial -- dial' spin-count
: spin-right
    0
    [ over 100 > ] [
        [ (spin-right) ] dip 1+
    ] while
    [ 100 mod ] dip ;

/// dial n -- dial' zero-count
: right-zeros
    +
    spin-right
    on-zero-1+ ;

/// dial line-pair -- dial-zero
: turn-dial-zero
    first2 swap
    left? [ left-zeros ] [ right-zeros ] if
    2vector ;

/// dial-log-pairs x -- dial x
: get-current-dial-from-pair
    [ dup last first ] dip ;

/// log pair -- log'
: accum-dial-zero
    get-current-dial-from-pair
    turn-dial-zero
    log-dial ;

/// lines -- zero-clicks
: ???-???   { { 50 0 } } [ accum-dial-zero ] reduce ;

/// zero-clicks -- checksum
: moar-profit   [ second ] map sum ;

/// lines -- zero-count
: debug-zero
        { { 50 0 } } [
        // log line -- log'
        dup print
        parse-line
        print-col
        accum-dial-zero
        dup 10 lastn print-col
        nl drop
    ] reduce
    moar-profit ;

/// data/str -- checksum/int
: part2   parse-input ???-??? moar-profit ;

/// -- checksum/int
: run-sample-2   2025 01 read-sample-data part2 ;
: run-data-2     2025 01 read-data        part2 ;

// 2025 01 read-sample-data lines debug-zero
run-data-2
