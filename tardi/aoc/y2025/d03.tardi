
uses: ../io
uses: std/io
uses: std/hashmaps
uses: std/math
uses: std/strings
uses: std/vectors

exports:
  but-last max-index
  parse-input
  select-first
  select-second
  select-digits
  find-max-joltage
  part1
  part2 ;


: parse-line    >utf8 [ utf8>digit ] map ;   /// str -- vector/int
: parse-input   lines [ parse-line ] map ;   /// str -- vector/vector/int

: from       0 swap ;
: to         pick vectors/length swap - ;
: but-last   from to rot subvector ;   /// vector/int n -- vector/int'

: extract-max   [ dup first ] dip ;   /// accum n -- accum max n

/// { max max-index index } n -- { n index index }
: set-max!
  0 pick set-nth!
  dup third
  1 pick set-nth! ;
/// { max max-index index } -- { max max-index index' }
: inc-index!
  dup third
  1+
  2 pick set-nth! ;

: is-greater?   [ < ] keep swap ;

/// vector/int -- max index
: max-index
  { 0 -1 0 } [
    extract-max
    is-greater? [
      set-max!
    ] [
      drop
    ] if
    inc-index!
  ] reduce
  first2 ;

/// vector/int -- highest-digit index
: select-first
  1 but-last
  max-index ;

: math/max         [ > ] 2keep ? ;                          /// x y -- xy

: max   dup first [ math/max ] reduce ;   /// xs -- x

/// xs -- max
: select-second   max ;

: push-max!   [ over push! ] dip ;   /// xs x i -- xs' i

: (crop-digits)   over vectors/length rot subvector ;   /// ds i -- ds'
: crop-digits     -rot [ (crop-digits) ] 2dip ;         /// ds c a i -- ds' c a

: select-iter      [ 1- ] dip ;   /// c x -- c' x
: select-cleanup   2dropd ;       /// ds c x -- x

: digits-to-search   pick pick 1- but-last ;   /// ds c a -- ds c a ds'

/// digits count -- xs
: select-digits
  { }
  [ over 0 > ] [
    digits-to-search
    max-index 1+
    push-max!
    crop-digits
    select-iter
  ] while
  select-cleanup ;

: pair>joltage   0 [ [ 10 * ] dip + ] reduce ;   // xs -- x

/// digits count -- n
: find-max-joltage   select-digits pair>joltage ;

: debug    dup print "\t" print ;      /// x -- x
: 2debug   2dup 2vector debug drop ;   /// x y -- x y

: split-prefix       0 -rot subvector ;                    /// i xs -- pref
: split-post         [ vectors/length ] keep subvector ;   /// i xs -- post
: vectors/split-at   [ split-prefix ] 2keep split-post ;   /// i xs -- xs1 xs2

/// x y ns --
: debug-slices
  swap
  [
    [ index-of? ] keep
    [ 0 -rot subvector debug drop ] 2keep
    [ vectors/length ] keep
    subvector
  ] dip
  swap
  [ index-of? ] keep
  vectors/split-at
  [ debug drop ] bi@ ;

/// data/str -- int
: debug-part1
  lines [
    // debug
    parse-line
    // debug
    [ select-digits ] keep
    2dupd debug-slices
    // 2debug
    pair>joltage
    debug
    nl
  ] map
  sum ;

/// data/str -- checksum/int
: part1
  parse-input
  [ 2 find-max-joltage ] map
  sum ;

/// -- checksum/int
: run-sample-1   2025 03 read-sample-data part1 ;
: run-data-1     2025 03 read-data        part1 ;

///////////////////////////

/// data/str -- checksum/int
: part2
  parse-input
  [ 12 find-max-joltage ] map
  sum ;

/// -- checksum/int
: run-sample-2   2025 03 read-sample-data part2 ;
: run-data-2     2025 03 read-data        part2 ;

// run-sample-1 .
// run-data-1 .
// run-sample-2 .
// run-data-2 .

// 2025 03 read-data debug-part1 .
