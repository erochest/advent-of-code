
uses: ./io
uses: std/io
uses: std/math
uses: std/strings
uses: std/vectors

exports:
  parse ??? profit
  parse-report
  first2 1vector #f? window-pairs
  safe? filter tri all-in-range? all-positive? all-negative?
  all? negative? positive? or and find-deltas window-pairs
  [..) [0..) remove-at
  partition dampener-safe?
  part1 part2 ;

/// string -- vector/vector/int
: parse-level    parse-int ;
: parse-report   split-whitespace [ parse-level ] map ;
: parse          lines [ parse-report ] map ;

/// vector/vector -- vector
: find-deltas       window-pairs [ first2 swap - ] map ;

: all-positive?   [ positive? ] all? ;
: all-negative?   [ negative? ] all? ;

: and-over   [ reach >= and ] keep ;   /// min x accum item -- min x accum' item
: and-under  pick < and ;              /// max accum item -- max accum'

/// xs min max -- ?
: all-in-range?
    rot #t [
        abs
        and-over
        and-under
    ] reduce
    [ 2drop ] dip ;

/// xs -- ?
: safe?
    find-deltas 
    [ all-positive? ]
      [ all-negative? ]
      [ 1 4 all-in-range? ] tri
    [ or ] dip and ;

/// vector/vector/int -- vector'/vector/int
: ???               [ safe? ] filter ;

/// vector -- checksum
: profit   length ;

: part1    parse ??? profit ;   /// string -- checksum

// TODO: neovim formatter for lining up these tables

/// xs -- ?
: dampener-safe?
    dup length [0..) #f [
        // xs ? i -- xs ?
        overd remove-at safe? or
    ] reduce nip ;

: problem-dampener   [ dampener-safe? ] filter ;   /// xs -- xs'

/// xs -- xs'
: ???
    [ safe? ] partition
    [ problem-dampener ] dip
    concat ;

: part2    parse ??? profit ;   /// string -- checksum

// TODO: main function that's only called when run from command-line

/////////////////////////

2024 2 read-sample-data

[
  "Sample Part 1: " print
  part1 println
]
[
  "Sample Part 2: " print
  part2 println
] bi

/////////////////////////

// TODO: kinda slow on full data. profiling

2024 2 read-data

[
  "Data Part 1: " print
  part1 println
]
[
  "Sample Part 2: " print
  part2 println
] bi

