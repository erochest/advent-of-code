
uses: ./io
uses: std/io
uses: std/hashmaps
uses: std/strings
uses: std/vectors

exports:
    rotate-matrix
    parse-line parse-lines
    sort-inputs pair-distances
    parse-input ??? profit
    part1 part2 ;

/// list-list-int -- list-int list-int
: rotate-matrix
    { } { } rot
    [
        // xs ys pair
        [ first ] [ second ] bi
        deep-push!
        deep-push!
    ] each ;

// TODO: to an aoc/parsing library
: parse-line    split-whitespace [ parse-int ] map ;   /// s -- ints
: parse-lines   [ parse-line ] map ;                   /// s -- list-list-int

: sort-inputs      2dup [ sort! ] bi@ ;   /// xs ys -- xs' ys'
: pair-distances   [ - abs ] map2 ;       /// xs ys -- zs

: parse-input   lines parse-lines rotate-matrix ;   /// string -- left-numbers right-numbers
: ???           sort-inputs pair-distances ;        /// left-numbers right-numbers -- list
: profit        sum ;                               /// list -- int

: part1         parse-input ??? profit ;       /// string -- checksum

//////////////////////

/// hm n -- hm v
: get-default
    over get
    [ drop 0 ] unless ;

/// v n -- pair
: >pair   swap 2vector ;

/// list hashmap -- list-pairs
: match-frequencies
    swap
    [
        // frequencies x
        [ get-default ] keep
        >pair
    ] map
    nip ;

/// list-pairs -- multiple-list
: multiply-pairs
    [ [ first ] [ second ] bi * ] map ;

: ???     frequencies match-frequencies ;

/// list-pairs -- int
: profit  multiply-pairs sum ;

: part2   parse-input ??? profit ;

///////////////////////

// 2024 1 read-sample-data
// 2024 1 read-data
// dup

// "Part 1: " print
// part1 println

// "Part 2: " print
// part2 println
